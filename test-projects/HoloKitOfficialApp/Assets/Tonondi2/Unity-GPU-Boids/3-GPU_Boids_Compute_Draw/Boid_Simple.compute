#pragma kernel CSMain
#define GROUP_SIZE 64

#include "UnityCG.cginc"

float hash( float n )
{
	return frac(sin(n)*43758.5453);
}

// The noise function returns a value in the range -1.0f -> 1.0f
float noise1( float3 x )
{
	float3 p = floor(x);
	float3 f = frac(x);

	f       = f*f*(3.0-2.0*f);
	float n = p.x + p.y*57.0 + 113.0*p.z;

	return lerp(lerp(lerp( hash(n+0.0), hash(n+1.0),f.x),
					lerp( hash(n+57.0), hash(n+58.0),f.x),f.y),
				lerp(lerp( hash(n+113.0), hash(n+114.0),f.x),
					lerp( hash(n+170.0), hash(n+171.0),f.x),f.y),f.z);
}

struct Boid
{
	float3 position;
	float3 direction;
	float noise_offset;
	float3 padding;
};

struct Obstacle
{
	float3 position;
	float scale;
	float sum;
};


RWStructuredBuffer<Boid> boidBuffer;
RWStructuredBuffer<Obstacle> obstacleBuffer;

CBUFFER_START(Params)
	float DeltaTime;
	float RotationSpeed;
	float BoidSpeed;
	float BoidSpeedVariation;
	float3 FlockPosition;
	float NeighbourDistance;
	uint BoidsCount;
CBUFFER_END

[numthreads(GROUP_SIZE,1,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
	uint instanceId = id.x; // bird id, same as the number of bird
	Boid boid = boidBuffer[instanceId];

	float noise = clamp(noise1(_Time / 100.0 + boid.noise_offset), -1, 1) * 2.0 - 1.0;
	float velocity = BoidSpeed * (1.0 + noise * BoidSpeedVariation); // make a variety velocity?

	float3 boid_pos = boid.position; // set position
	float3 boid_dir = boid.direction; // set direction

	float3 avoid = float3(0.0, 0.0, 0.0);
	float3 separation = float3(0.0, 0.0, 0.0);
	float3 alignment = float3(0.0, 0.0, 0.0);
	float3 cohesion = FlockPosition;

	uint nearbyCount = 1; // Add self that is ignored in loop
	for (uint i = 0; i < BoidsCount; i++) {
		if (i == instanceId)
			continue;
		
		if (distance(boid_pos, boidBuffer[i].position) < NeighbourDistance)
		{
		// tempBoid = boidBuffer[i];
			float3 tempBoid_position = boidBuffer[i].position;

			float3 diff = boid_pos - tempBoid_position; // the vector away from neighbor
			float diffLen = (length(diff));
			float scaler = clamp(1.0 - diffLen / NeighbourDistance, 0.0, 1.0); // the far the distance, the small the force
			
			separation += diff * (scaler / diffLen);
			alignment += boidBuffer[i].direction;
			cohesion += tempBoid_position;

			nearbyCount += 1;
		}
	}

	for (uint i = 0; i < obstacleBuffer[0].sum; i++)
	{
		float3 diff = boid.position - obstacleBuffer[i].position; // the vector away from pbs
		float diffLen = (length(diff));
		float scaler = clamp(1.0 - diffLen / obstacleBuffer[i].scale, 0.0, 1.0); // the far the distance, the small the force
		avoid += 10 * diff * (scaler / diffLen);
	}


	float avg = 1.0 / nearbyCount;
	alignment *= avg;
	cohesion *= avg;
	cohesion = normalize(cohesion - boid_pos);

	float3 direction = alignment + separation + cohesion + avoid;

	float ip = exp(-RotationSpeed * DeltaTime);
	boid.direction = lerp((direction), normalize(boid_dir), ip);

	boid.position += (boid.direction) * (velocity * DeltaTime);

	boidBuffer[id.x] = boid;
}

